Program.Sub.ScreenSU.Start
Gui.Form1..Create
Gui.Form1..Caption("GCG 5617 - Quote Line Directory Maintenance")
Gui.Form1..Size(9615,2415)
Gui.Form1..MinX(0)
Gui.Form1..MinY(0)
Gui.Form1..Position(0,0)
Gui.Form1..BackColor(-2147483633)
Gui.Form1..MaxButton(False)
Gui.Form1..MousePointer(0)
Gui.Form1..Sizeable(False)
Gui.Form1..Event(UnLoad,Form1_UnLoad)
Gui.Form1.txtBaseDir.Create(TextBox,"",True,6645,300,0,2055,195,False,0,"Arial",8,-2147483643,1)
Gui.Form1.lbl1.Create(Label,"Base Directory",True,1320,255,0,165,270,True,0,"Arial",8,-2147483633,0)
Gui.Form1.lbl2.Create(Label,"Template Directory",True,1575,255,0,180,660,True,0,"Arial",8,-2147483633,0)
Gui.Form1.cmdSave.Create(Button)
Gui.Form1.cmdSave.Size(855,375)
Gui.Form1.cmdSave.Position(4305,1290)
Gui.Form1.cmdSave.Caption("Save")
Gui.Form1.cmdSave.Event(Click,cmdSave_Click)
Gui.Form1.txtTemplateDir.Create(TextBox,"",True,6645,300,0,2055,585,False,0,"Arial",8,-2147483643,1)
Gui.Form1.cmdBrowseBaseDir.Create(Button)
Gui.Form1.cmdBrowseBaseDir.Size(555,375)
Gui.Form1.cmdBrowseBaseDir.Position(8745,135)
Gui.Form1.cmdBrowseBaseDir.Caption("^")
Gui.Form1.cmdBrowseBaseDir.Event(Click,cmdBrowseBaseDir_Click)
Gui.Form1.cmdBrowseTemplateDir.Create(Button)
Gui.Form1.cmdBrowseTemplateDir.Size(555,375)
Gui.Form1.cmdBrowseTemplateDir.Position(8745,555)
Gui.Form1.cmdBrowseTemplateDir.Caption("^")
Gui.Form1.cmdBrowseTemplateDir.Event(Click,cmdBrowseTemplateDir_Click)
Gui.Form1.lbl3.Create(Label,"Replacement File Text",True,1575,450,0,165,1020,True,0,"Arial",8,-2147483633,0)
Gui.Form1.txtReplacementText.Create(TextBox,"",True,1650,300,0,2055,975,True,0,"Arial",8,-2147483643,1)
Gui.Form1.chkView.Create(CheckBox)
Gui.Form1.chkView.Enabled(False)
Gui.Form1.chkView.Size(1560,360)
Gui.Form1.chkView.Position(7950,1440)
Gui.Form1.chkView.Caption("View Created")
Gui.Form1.cmdCreateView.Create(Button)
Gui.Form1.cmdCreateView.Size(1155,375)
Gui.Form1.cmdCreateView.Position(6720,1425)
Gui.Form1.cmdCreateView.Caption("Create View")
Gui.Form1.cmdCreateView.DisableOnClick(11)
Gui.Form1.cmdCreateView.Event(Click,cmdCreateView_Click)
Program.Sub.ScreenSU.End

Program.Sub.Preflight.Start
Program.Sub.Preflight.End

Program.Sub.Main.Start
'Author: Daniel Duncan
'Customer: Sterling Process Eng. & Serv.
'Program Name: Quote Line Directory Maintenance
'Date Started: 8/6/2019
'Description: This project is placed in Estimating / Routing & Quote Management.  This program allows the user to maintain base and template directories, which are used in the other script in this project to store files relates to each quote.  The screen also allows the user to maintain the replacement text, which is used when copying and renaming files.
'Quote: 11531-0
'GAP: 5617
f.Intrinsic.Control.Try

	f.Intrinsic.Control.CallSub(GetData)
	gui.Form1..Show
	
f.Intrinsic.Control.Catch
	f.Intrinsic.Control.CallSub(ErrorMsg, CurrentSub, v.Ambient.CurrentSubroutine)
f.Intrinsic.Control.EndTry
Program.Sub.Main.End

Program.Sub.Form1_UnLoad.Start
	f.Intrinsic.Control.End
Program.Sub.Form1_UnLoad.End

Program.Sub.cmdSave_Click.Start
f.Intrinsic.Control.Try
	v.Local.sSQL.Declare(string)
	
	f.Intrinsic.String.Build("insert into GCG_5617_QTEDIRMNT values ('{0}','{1}','{2}');",v.Screen.Form1!txtBaseDir.text,v.Screen.Form1!txtTemplateDir.text,v.Screen.Form1!txtReplacementText.text,v.Local.sSQL)
	f.ODBC.Connection!con.opencompanyconnection
	f.ODBC.Connection!con.execute("delete from GCG_5617_QTEDIRMNT;")
	f.ODBC.Connection!con.execute(v.Local.sSQL)
	f.ODBC.Connection!con.close
	f.Intrinsic.UI.Msgbox("Values saved successfully")
	
f.Intrinsic.Control.Catch
	f.Intrinsic.Control.CallSub(ErrorMsg, CurrentSub, v.Ambient.CurrentSubroutine)
f.Intrinsic.Control.EndTry
Program.Sub.cmdSave_Click.End

Program.Sub.GetData.Start
f.Intrinsic.Control.Try
	V.Local.bExists.Declare(Boolean)

	f.ODBC.Connection!con.opencompanyconnection
	f.Data.DataTable.CreateFromSQL("dtData","con","select * from GCG_5617_QTEDIRMNT")
	
	f.Intrinsic.Control.If(v.DataTable.dtData.RowCount,>,0)
		gui.Form1.txtBaseDir.Text(v.DataTable.dtData(0).BaseDir!FieldValTrim)
		gui.Form1.txtTemplateDir.Text(v.DataTable.dtData(0).TemplateDir!FieldValTrim)
		gui.Form1.txtReplacementText.Text(v.DataTable.dtData(0).ReplacementString!FieldValTrim)
	f.Intrinsic.Control.EndIf
	
	'Check if our view already exists. If so hide the button and check the checkbox
	F.ODBC.Connection!con.TableExists("V_GCG_5617_Q_JOIN",V.Local.bExists)
	
	F.Intrinsic.Control.If(V.Local.bExists,=,True)
		Gui.Form1.cmdCreateView.Visible(False)
		Gui.Form1.chkView.Value(1)
	F.Intrinsic.Control.EndIf
	
	f.ODBC.Connection!con.Close
	
f.Intrinsic.Control.Catch
	f.Intrinsic.Control.CallSub(ErrorMsg, CurrentSub, v.Ambient.CurrentSubroutine)
f.Intrinsic.Control.EndTry
Program.Sub.GetData.End

Program.Sub.ErrorMsg.Start
v.Local.sError.Declare

'Closes ODBC connection, default to connection : "con"
f.Intrinsic.Control.If(v.ODBC!con.State, =, 1)
	f.ODBC.Connection!con.Close
f.Intrinsic.Control.EndIf

'Generic Error message.
f.Intrinsic.String.Build("Project GCG_5617_QuoteLineDirectoryMaintenance.g2u {0}{0}Subroutine: {1}{0}Error: {2} with Description: {3}", v.Ambient.NewLine, v.Args.CurrentSub, v.Ambient.ErrorNumber, v.Ambient.ErrorDescription, v.Local.sError)
f.Intrinsic.UI.Msgbox(v.Local.sError)
Program.Sub.ErrorMsg.End

Program.Sub.cmdBrowseBaseDir_Click.Start
f.Intrinsic.Control.Try
	v.Local.sSelectedFileName.Declare(string)
	
	f.Intrinsic.UI.FolderBrowser("Browse for folder",v.Local.sSelectedFileName)
	
	f.Intrinsic.Control.If(v.Local.sSelectedFileName,!=,"***CANCEL***")
		gui.Form1.txtBaseDir.Text(v.Local.sSelectedFileName)
	F.Intrinsic.Control.EndIf
	
f.Intrinsic.Control.Catch
	f.Intrinsic.Control.CallSub(ErrorMsg, CurrentSub, v.Ambient.CurrentSubroutine)
f.Intrinsic.Control.EndTry
Program.Sub.cmdBrowseBaseDir_Click.End

Program.Sub.cmdBrowseTemplateDir_Click.Start
f.Intrinsic.Control.Try
	v.Local.sSelectedFileName.Declare(string)
	f.Intrinsic.UI.FolderBrowser("Browse for folder",v.Local.sSelectedFileName)
	
	f.Intrinsic.Control.If(v.Local.sSelectedFileName,!=,"***CANCEL***")
		gui.Form1.txtTemplateDir.Text(v.Local.sSelectedFileName)
	F.Intrinsic.Control.EndIf
	
f.Intrinsic.Control.Catch
	f.Intrinsic.Control.CallSub(ErrorMsg, CurrentSub, v.Ambient.CurrentSubroutine)
f.Intrinsic.Control.EndTry
Program.Sub.cmdBrowseTemplateDir_Click.End

Program.Sub.cmdCreateView_Click.Start
F.Intrinsic.Control.Try
	V.Local.bExists.Declare(Boolean)
	
	F.ODBC.Connection!con.OpenCompanyConnection	
	
	'Check to see if this view exists already. If not attempt to create
	F.ODBC.Connection!con.TableExists("V_GCG_5617_Q_JOIN",V.Local.bExists)
	
	F.Intrinsic.Control.If(V.Local.bExists,<>,True)
		F.ODBC.Connection!con.Execute("CREATE VIEW V_GCG_5617_Q_JOIN AS SELECT V_QUOTE_LINES.QUOTE_NO ,V_QUOTE_LINES.RECORD_NO ,V_QUOTE_LINES.User_1 ,V_QUOTE_HEADER.DATE_QUOTE FROM V_QUOTE_HEADER V_QUOTE_HEADER LEFT JOIN V_QUOTE_LINES V_QUOTE_LINES ON V_QUOTE_HEADER.Quote_No = V_QUOTE_LINES.Quote_No")
		
		F.ODBC.Connection!con.TableExists("V_GCG_5617_Q_JOIN",V.Local.bExists)
		'If creation fails inform the user to contact global shop, otherwise inform the user it was created and hide the button to create.
		F.Intrinsic.Control.If(V.Local.bExists,<>,True)
			F.Intrinsic.UI.Msgbox("View failed to create. Please contact Global Shop Solutions.")
			F.Intrinsic.Control.ExitSub
		F.Intrinsic.Control.Else
			F.Intrinsic.UI.Msgbox("View created successfully.")
			Gui.Form1.chkView.Value(1)
			Gui.Form1.cmdCreateView.Visible(False)
		F.Intrinsic.Control.EndIf		
	F.Intrinsic.Control.EndIf
	
	F.ODBC.Connection!con.Close
	
F.Intrinsic.Control.Catch
	f.Intrinsic.Control.CallSub(ErrorMsg, CurrentSub, v.Ambient.CurrentSubroutine)
F.Intrinsic.Control.EndTry
Program.Sub.cmdCreateView_Click.End
